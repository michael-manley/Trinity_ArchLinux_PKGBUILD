# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-dbus-1-tqt
pkgver=14.0.4
pkgrel=1
pkgdesc="Trinity D-Bus TQtInterface Wrapper - from TDE upstream GIT"
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('tde-core' 'tde-base')
depends=('tde-tqtinterface' 'dbus')
makedepends=('pkgconfig' 'cmake')
provides=('dbus-1-tqt')
conflicts=('trinity-dbus-1-tqt')
replaces=('trinity-dbus-1-tqt' 'trinity-dbus-1-qt3' 'dbus-1-qt3' 'tde-dbus-tqt-1')
options=('staticlibs' 'libtool' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/dependencies/dbus-1-tqt-R${pkgver}.tar.bz2")
md5sums=('202b9752cf7bdfd4542ae36e95248233')
install='trinity-dbus-1-tqt.install'

build() {
   msg "Setting PATH, CMAKE and Trinity Environment variables"
   [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
   [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

   cd $srcdir
   msg "Creating out-of-source build directory: ${srcdir}/${_builddir}"
   mkdir -p build
   cd build

   msg "Starting cmake..."
   cmake ${srcdir}/dependencies/${pkgname#*-} \
      -DCMAKE_INSTALL_PREFIX=${TDEDIR}

   msg "Building - $pkgname..."
   make $NUMJOBS

}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   make -j1 DESTDIR="${pkgdir}" install
}
