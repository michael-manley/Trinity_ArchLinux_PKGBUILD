# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-tdeartwork
pkgver=14.0.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/tdeartwork'
license=('GPL')
groups=('tde-base')
pkgdesc="Trinity Desktop Development Ingegration Components - TDE upstream GIT"
depends=('tde-tdebase' 'xscreensaver' 'libxss')
makedepends=('pkgconfig' 'cmake')
optdepends=()
provides=('tdeartwork')
conflicts=('trinity-tdeartwork' 'trinity-kdeartwork')
replaces=('trinity-tdeartwork')
options=('staticlibs' 'libtool' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/tdeartwork-R${pkgver}.tar.bz2")
md5sums=('c9935804a3dd201deaa8960e922a24d3')
# install=''

build() {
  msg "Setting PATH, CMAKE and Trinity Environment variables"
  # Source the QT and TDE profile
  [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
  [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

  cd $srcdir

  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p build
  cd build

  msg "Starting cmake..."
  cmake ${srcdir}/${pkgname#*-} \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
    -DWITH_ARTS=ON \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DBUILD_ALL=ON

  msg "Building - ${pkgname#*-}..."
  make $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build
  make -j1 DESTDIR="${pkgdir}" install
  msg "Removing conflicting images"  
  rm ${pkgdir}/opt/trinity/share/icons/locolor/16x16/apps/kbabel.png
  rm ${pkgdir}/opt/trinity/share/icons/locolor/32x32/apps/kbabel.png
  rm ${pkgdir}/opt/trinity/share/icons/locolor/16x16/apps/ktimemon.png
  rm ${pkgdir}/opt/trinity/share/icons/locolor/32x32/apps/ktimemon.png
}
