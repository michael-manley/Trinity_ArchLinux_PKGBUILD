# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-arts
pkgver=14.0.4
pkgrel=1
pkgdesc="Trinity ARTS Sound System and libraries"
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/arts'
license=('GPL')
groups=('tde-core' 'tde-base')
depends=('alsa-plugins'
	'alsa-tools'
	'alsa-utils'
	'audiofile'
	'glib2'
	'libmad'
	'libogg'
	'libvorbis'
	'pulseaudio-alsa'
	'tde-tqtinterface')
makedepends=('pkgconfig' 'cmake')
optdepends=('audio-convert: A script with an easy to use interface to convert audio files: wav, mp3, ogg, flac, aac, mpc, ape and wma'
	'lame: A high quality MPEG Audio Layer III (MP3) encoder'
	'libmp3splt: Library for splitting mp3 and ogg files without decoding'
	'mpd: Music daemon that plays MP3, FLAC, and Ogg Vorbis files'
	'vorbis-tools: Extra tools for Ogg-Vorbis')
provides=('arts')
conflicts=('kdemod3-arts' 'arts')
replaces=('trinity-arts')
options=('staticlibs' 'libtool' '!emptydirs')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/dependencies/arts-R${pkgver}.tar.bz2")
md5sums=('c27b72aee60c7aac6e415fbc30d387e2')
install='tde-arts.install'

build() {
   msg "Setting PATH, CMAKE and Trinity Environment variables"
   # Source the QT and KDE profile
   [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
   [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

   cd $srcdir
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p build
   cd build

   msg "Starting cmake..."
   cmake ${srcdir}/dependencies/arts/ \
      -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
      -DWITH_GCC_VISIBILITY=ON

   msg "Building $pkgname..."
   make $NUMJOBS

}

package() {
   msg "Packaging - $pkgname-$pkgver"

   cd ${srcdir}/build
   make -j1 DESTDIR="$pkgdir" install

}
