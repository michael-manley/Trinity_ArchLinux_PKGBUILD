# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-dbus-tqt
pkgver=14.0.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/dbus-tqt'
license=('GPL')
groups=('tde-core' 'tde-base')
pkgdesc="Trinity TQt DBus wrapper"
depends=('tde-arts' 'dbus')
makedepends=('pkgconfig' 'cmake')
provides=('dbus-tqt')
options=('staticlibs' 'libtool' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/dependencies/dbus-tqt-R${pkgver}.tar.bz2")
md5sums=('7508ac69693b77cd87725a0500d1333f')
install='trinity-dbus-tqt.install'

build() {
   msg "Setting PATH, CMAKE and Trinity Environment variables"
   # Source the QT and TDE profile
   [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
   [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

   cd $srcdir
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p build
   cd build

   msg "Starting cmake..."
   cmake ${srcdir}/dependencies/${pkgname#*-} \
      -DCMAKE_INSTALL_PREFIX=$TDEDIR

   msg "Building - $pkgname..."
   make $NUMJOBS
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   make -j1 DESTDIR="${pkgdir}" install
}
