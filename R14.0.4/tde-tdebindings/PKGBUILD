# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-tdebindings
pkgver=14.0.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/tdebindings'
license=('GPL')
groups=('tde-base' 'tde-dev')
pkgdesc="Trinity Desktop bindings"
depends=('tde-tdebase')
makedepends=('pkgconfig' 'cmake' 'python2')
options=('staticlibs' 'libtool' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/tdebindings-R${pkgver}.tar.bz2" 
        "tdebindings-perl-fix.patch")
md5sums=('bbb06cfe4a73054d7735980dfc279f20'
         'ce7c6b17f180f08afae5b123465c3952')
# install=''

build() {
  msg "Setting PATH, CMAKE and Trinity Environment variables"
  # Source the QT and TDE profile
  [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
  [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

  ## Generate config files and update with autoreconf
  cd ${srcdir}/${pkgname#*-}

  patch -p1 -i ${srcdir}/tdebindings-perl-fix.patch

  msg "Copying system libtool files...."
  cp /usr/share/aclocal/libtool.m4 ./admin/libtool.m4.in
  cp /usr/share/libtool/build-aux/ltmain.sh ./admin/ltmain.sh

  msg "Running make -f admin/Makefile.common ...."
  make -f admin/Makefile.common

  ## configure
  msg "Configuring - ${pkgname}..."
  PYTHON=/usr/bin/python2 \
  ./configure \
    --prefix=${TDEDIR} \
    --with-qt-dir=${QTDIR} \
    --with-qt-includes=${QTDIR}/include \
    --with-qt-libraries=${QTDIR}/lib \
    --with-extra-includes="/usr/include/tqt:/usr/include/tqt/Qt" \
    --with-extra-libs="${TDEDIR}/lib:${TDEDIR}/lib/trinity" \
    --sysconfdir=${TDEDIR}/etc \
    --localstatedir=/var \
    --enable-closure

  msg "Building - ${pkgname}..."
  make $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/${pkgname#*-}

  make -j1 DESTDIR="${pkgdir}" install
}
