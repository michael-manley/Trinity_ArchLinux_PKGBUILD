# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-tqca-tls
pkgver=14.0.4
pkgrel=1
pkgdesc="Trinity SSL/TLS plugin for TQt Cryptographic Architecture (TQCA)"
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/tqca-tls'
license=('GPL')
groups=('tde-core' 'tde-base')
depends=('tde-tqtinterface' 'openssl-1.0')
makedepends=('pkgconfig')
provides=('tqca-tls')
conflicts=('trinity-tqca-tls')
replaces=('trinity-tqca-tls')
options=('staticlibs' 'libtool' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/dependencies/tqca-tls-R${pkgver}.tar.bz2")
md5sums=('f2b3a28ab6e4bc2ebc0fa98c9997086b')

build() {
  msg "Setting PATH and Trinity Environment variables"
  [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
  [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

  cd ${srcdir}/dependencies/${pkgname#*-}

  msg "Starting configure..."
  ./configure --qtdir=${QTDIR} --with-openssl-inc=/usr/include/openssl-1.0 \
     --with-openssl-lib=/usr/lib/openssl-1.0

  msg "Building - $pkgname..."
  make  || return 1
}

package() {
  msg "Packaging - $pkgname"
  cd ${srcdir}/dependencies/${pkgname#*-}
  make INSTALL_ROOT="${pkgdir}" install
}
