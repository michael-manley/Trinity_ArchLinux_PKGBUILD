# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-tdevelop
pkgver=14.0.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/tdevelop'
license=('GPL')
groups=('tde-base')
pkgdesc="Trinity Desktop Developer Tools"
depends=('tde-tdebase')
makedepends=('pkgconfig' 'cmake')
provides=('tdevelop')
conflicts=('trinity-tdevelop' 'trinity-kdevelop')
replaces=('trinity-tdevelop')
options=('staticlibs' 'libtool' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/tdevelop-R${pkgver}.tar.bz2"
        "fix_tqt_error.patch")
md5sums=('173c677acbbf44155b4f242e69174523'
         'da7c0a4793e98351d9f2d4122d3d2626')
# install=''

build() {
   msg "Setting PATH, CMAKE and Trinity Environment variables"
   # Source the QT and TDE profile
   [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
   [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

   cd ${srcdir}/${pkgname#*-}
   patch -p1 -i ${srcdir}/fix_tqt_error.patch

   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p ${srcdir}/build
   cd ${srcdir}/build

   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-} \
      -DCMAKE_VERBOSE_MAKEFILE=ON \
      -DCMAKE_INSTALL_PREFIX=$TDEDIR \
      -DWITH_BUILDTOOL_ALL=ON \
      -DWITH_LANGUAGE_ALL=ON \
      -DWITH_VCS_ALL=ON \
      -DBUILD_ALL=ON

   msg "Building - ${pkgname#*-}..."
   make $NUMJOBS
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   make -j1 DESTDIR="${pkgdir}" install
}
