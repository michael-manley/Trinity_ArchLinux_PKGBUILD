# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-tdemultimedia
pkgver=14.0.4
pkgrel=1
pkgdesc="Trinity Desktop MultiMedia Components"
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/tdemultimedia'
license=('GPL')
groups=('tde-extra')
depends=('tde-tdebase')
makedepends=('pkgconfig' 'cmake')
provides=('tdemultimedia')
conflicts=('trinity-tdemultimedia' 'trinity-kdemultimedia')
replaces=('trinity-tdemultimedia')
options=('staticlibs' 'libtool' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/tdemultimedia-R${pkgver}.tar.bz2")
md5sums=('5b5398adbd2a01297ae90a84c9ddf909')
# install=''

build() {
  msg "Setting PATH, CMAKE and Trinity Environment variables"
  # Source the QT and TDE profile
  [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
  [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

  ## Generate config files and update with autoreconf
  cd ${srcdir}/${pkgname#*-}

  msg "Copying system libtool files...."
  cp /usr/share/aclocal/libtool.m4 ./admin/libtool.m4.in
  cp /usr/share/libtool/build-aux/ltmain.sh ./admin/ltmain.sh

  msg "Running make -f admin/Makefile.common ...."
  make -f admin/Makefile.common

  ## configure
  msg "Configuring - ${pkgname}..."
  CFLAGS="${CFLAGS} -fpermissive" \
  CXXFLAGS="${CXXFLAGS} -fpermissive" \
  ./configure \
    --prefix=${TDEDIR} \
    --with-qt-dir=${QTDIR} \
    --with-qt-includes=${QTDIR}/include \
    --with-qt-libraries=${QTDIR}/lib \
    --sysconfdir=${TDEDIR}/etc \
    --localstatedir=/var \
    --enable-closure

  msg "Building - ${pkgname#*-}..."
  make $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/${pkgname#*-}            # use for libtool

  make -j1 DESTDIR="${pkgdir}" install
}
