# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-libkdcraw
pkgver=14.0.4
pkgrel=1
pkgdesc="The EXIV2 Library interface for TDE kipi-plugins"
arch=('i686' 'x86_64')
url="http://scm.trinitydesktop.org/scm/git/${pkgname#*-}"
license=('GPL')
groups=('tde-base')
depends=('tde-tdebase' 'libraw' 'lcms')
makedepends=('pkgconfig' 'autoconf' 'imake')
optdepends=()
provides=("${pkgname#*-}")
conflicts=("trinity-${pkgname#*-}")
replaces=("trinity-${pkgname#*-}")
options=('staticlibs' 'libtool' '!emptydirs')
# install='pkgname.install'
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/libraries/libkdcraw-R${pkgver}.tar.bz2")
md5sums=('8ba1d417f43d1ce20950314b033d75f0')


build() {

  cd ${srcdir}

  ## Environment config
  msg "Setting PATH, Trinity Environment variables"
  # Source the QT and TDE profile
  [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh       # or tqt3.sh
  [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh   # or tde.sh

  ## Generate config files and update with autoreconf
  cd ${srcdir}/libraries/${pkgname#*-}

  msg "Copying system libtool files...."
  cp /usr/share/aclocal/libtool.m4 ./admin/libtool.m4.in
  cp /usr/share/libtool/build-aux/ltmain.sh ./admin/ltmain.sh

  msg "Running autotools, libtoolize, aclocal, autoupdate, autoconf, automake, autoreconf"
  aclocal
  autoupdate
  autoconf
  automake --add-missing

  msg "Running make -f admin/Makefile.common ...."
  make -f admin/Makefile.common

  ## configure
  msg "Configuring - ${pkgname}..."

  ./configure \
    --prefix=${TDEDIR} \
    --with-qt-dir=${QTDIR} \
    --with-qt-includes=${QTDIR}/include \
    --with-qt-libraries=${QTDIR}/lib \
    --localstatedir=/var \
    --enable-debug=full \
    --enable-closure

  ## make $NUMJOBS
  msg "Building - ${pkgname}..."
  make $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"

  cd ${srcdir}/libraries/${pkgname#*-}            # use for non-out-of-source

  make -j1 DESTDIR="$pkgdir" install
}
