# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-tdelibs
pkgver=14.0.5
pkgrel=1
pkgdesc="Trinity Desktop core libraries and applications"
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/tdelibs'
license=('GPL')
groups=('tde-core')
depends=('aspell'
	'hspell'
	'jasper'
	'libcups'
	'libidn'
	'libutempter'
	'libxcomposite'
	'libxslt'
	'libxtst'
	'openssl-1.0'
	'openexr'
	'pcre'
	'tde-arts'
	'tde-libart-lgpl'
	'xorg-iceauth'
	'xorg-xmessage'
	'xorg-xprop'
	'xorg-xset'
	'xorg-xsetroot'
	'tde-dbus-1-tqt')
makedepends=('pkgconfig' 'cmake')
provides=('tdelibs')
conflicts=('trinity-kdelibs' 'trinity-tdelibs' 'kdemod3-kdelibs' 'kdelibs3')
replaces=('trinity-kdelibs' 'trinity-tdelibs')
options=('staticlibs' 'libtool' '!makeflags' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/tdelibs-R${pkgver}.tar.bz2")

md5sums=('03e4bdd2f36d1f5df1427b731a7c75ae')

install='trinity-tdelibs.install'

build() {
  msg "Setting PATH, CMAKE and Trinity Environment variables"
  # Source the QT and TDE profile
  [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
  [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

  cd "${srcdir}/tdelibs"

  cd $srcdir
  msg "Creating out-of-source build directory: ${srcdir}/${_builddir}"
  mkdir -p build
  cd build

  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig

  msg "Starting cmake..."
  cmake ${srcdir}/tdelibs \
    -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
    -DSYSCONF_INSTALL_DIR=${TDEDIR}/etc \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DWITH_ARTS=ON \
    -DWITH_ALSA=ON \
    -DWITH_LIBART=ON \
    -DWITH_LIBIDN=ON \
    -DWITH_SSL=ON \
    -DWITH_CUPS=ON \
    -DWITH_LUA=OFF \
    -DWITH_TIFF=ON \
    -DWITH_JASPER=ON \
    -DWITH_OPENEXR=ON \
    -DWITH_UTEMPTER=ON \
    -DWITH_AVAHI=ON \
    -DWITH_PAM=ON \
    -DWITH_PCRE=ON \
    -DWITH_GCC_VISIBILITY=ON \
    -DWITH_TDEHWLIB_DAEMONS=ON \
    -DWITH_UPOWER=ON \
    -DWITH_UDISKS2=ON \
    -DWITH_LZMA=ON \
    -DWITH_XRANDR=ON \
    -DWITH_XCOMPOSITE=ON \
    -DWITH_ASPELL=ON \
    -DWITH_HSPELL=ON

  msg "Building - $pkgname..."
  make $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build
  make -j1 DESTDIR="$pkgdir" install
  msg "Removing conflicting images"
  rm ${pkgdir}/opt/trinity/share/mimelnk/application/x-mplayer2.desktop
}
