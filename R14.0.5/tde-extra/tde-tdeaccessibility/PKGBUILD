# Maintainer: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-tdeaccessibility
pkgver=14.0.5
pkgrel=1
pkgdesc="Accessibility utilities for the TDE Desktop"
arch=('i686' 'x86_64')
url="http://scm.trinitydesktop.org/scm/git/${pkgname#*-}"
license=('GPL')
groups=('tde-extra')
depends=('tde-tdebase')
makedepends=('pkgconfig' 'cmake' 'autoconf' 'imake')
optdepends=()
provides=("${pkgname#*-}")
conflicts=("trinity-${pkgname#*-}")
replaces=("trinity-${pkgname#*-}")
options=('staticlibs' 'libtool' '!emptydirs')
# install='pkgname.install'
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/tdeaccessibility-R${pkgver}.tar.bz2")
md5sums=('e6b46859ddc8bb22e6b4e5cd21f989e1')

build() {

  cd ${srcdir}

  ## Set up environment
  msg "Setting PATH, Trinity Environment variables"
  # Source the QT and TDE profile
  [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh       # or tqt3.sh
  [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh   # or tde.sh

  cd ${srcdir}/${pkgname#*-}
  ## Generate config files and update with autoreconf

  msg "Copying system libtool files...."
  cp /usr/share/aclocal/libtool.m4 ./admin/libtool.m4.in
  cp /usr/share/libtool/build-aux/ltmain.sh ./admin/ltmain.sh

  msg "Running make -f admin/Makefile.common ...."
  make -f admin/Makefile.common

  ## configure
  msg "Configuring - ${pkgname}..."
  ./configure \
    --prefix=${TDEDIR} \
    --with-qt-dir=${QTDIR} \
    --with-extra-libs=${TDEDIR}/lib/trinity \
    --localstatedir=/var \
    --enable-kttsd-epos=no \
    --enable-kttsd-festivalint=no \
    --enable-kttsd-flite=no \
    --enable-kttsd-freetts=no \
    --enable-kttsd-hadifix=no \
    --enable-debug=full \
    --enable-closure

  msg "Building - ${pkgname}..."

  make $NUMJOBS

}

package() {
  msg "Packaging - $pkgname-$pkgver"

  cd ${srcdir}/${pkgname#*-}            # use for non-out-of-source

  make -j1 DESTDIR="$pkgdir" install
}
