# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-kmplayer
pkgver=14.0.5
pkgrel=1
pkgdesc="TDE KMPlayer"
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/applications/kmplayer'
license=('GPL')
groups=('tde-extra')
depends=('tde-tdebase' 'dbus-glib')
makedepends=('pkgconfig' 'cmake' 'mplayer')
#provides=('kmplayer')
#conflicts=('kmplayer')
#replaces=('kmplayer')
options=('staticlibs' 'libtool' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/applications/kmplayer-R${pkgver}.tar.bz2")
md5sums=('968e2116b333ea646ce00fd2ab2038a8')
# install=''

build() {
  msg "Setting PATH, CMAKE and Trinity Environment variables"
  # Source the QT and TDE profile
  [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
  [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

  ## Generate config files and update with autoreconf
  cd ${srcdir}/applications/${pkgname#*-}

  msg "Copying system libtool files...."
  cp /usr/share/aclocal/libtool.m4 ./admin/libtool.m4.in
  cp /usr/share/libtool/build-aux/ltmain.sh ./admin/ltmain.sh

  msg "Running make -f admin/Makefile.common ...."
  make -f admin/Makefile.common

  ## configure
  msg "Configuring - ${pkgname}..."
  CFLAGS="${CFLAGS} -fpermissive" \
  CXXFLAGS="${CXXFLAGS} -fpermissive" \
  ./configure \
    --prefix=${TDEDIR} \
    --with-qt-dir=${QTDIR} \
    --with-qt-includes=${QTDIR}/include \
    --with-qt-libraries=${QTDIR}/lib \
    --sysconfdir=${TDEDIR}/etc \
    --localstatedir=/var \
    --enable-closure

  msg "Building - ${pkgname#*-}..."
make $NUMJOBS
}

package() {
msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/applications/${pkgname#*-}            # use for libtool

make -j1 DESTDIR="${pkgdir}" install
}
